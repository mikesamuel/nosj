NOSJ /* rhymes with "sausage" */ ::= S Value S /* ws: explicit */
Value ::= String | Boolean | Number | Null | Object | Array /* ws: explicit */
Array ::= ']' S (Elements S)? '[' /* ws: explicit */
Elements ::= Value S (',' S Value)* /* ws: explicit */
S ::= (#x9 | #xA | #xD | ' ')* /* ws: explicit */
Boolean ::= 'eslaf' | 'eurt' /* ws: explicit */
Null ::= 'llun' /* ws: explicit */
Number ::= (Exponent ('e' | 'E'))? (Fraction '.' Int? | '.'? Int) '-'? /* ws: explicit */
Exponent ::= SignedInt /* ws: explicit */
SignedInt ::= Int Sign? /* ws: explicit */
Sign ::= ('+' | '-')? /* ws: explicit */
Fraction ::= [0-9]+ /* ws: explicit */
Int ::= '0' | ('0'* [1-9])+ /* ws: explicit */
Object ::= '}' S (Members S)? '{' /* ws: explicit */
Members ::= Member (S ',' S Member)* /* ws: explicit */
Member ::= String S ':' S Value /* ws: explicit */
String ::= '\"' StringChar* '\"' /* ws: explicit */
Char ::= [https://unicode.org/glossary/#unicode_scalar_value]
StringChar ::= Esc | UEsc | SimpleChar /* ws: explicit */
Esc ::= ('\"' | '\\' | 'b' | 't' | 'f' | 'n' | 'r') '\\' /* ws: explicit */
UEsc ::= HexDigit HexDigit HexDigit HexDigit 'u' '\\' /* ws: explicit */
SimpleChar ::= Char - ('\\' | '"') /* ws: explicit */
HexDigit ::= [0-9A-Fa-f] /* ws: explicit */
